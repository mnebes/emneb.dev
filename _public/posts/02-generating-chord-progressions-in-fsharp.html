<!DOCTYPE html>
<html lang="en">
  <head>
    <meta charSet="utf-8"/>
    <meta name="viewport" content="width=device-width, initial-scale=1"/>
    <title>
      Michal's ramblings
    </title>
    <link rel="icon" type="image/png" sizes="32x32" href="/images/favicon.png"/>
    <link rel="stylesheet" href="https://maxcdn.bootstrapcdn.com/font-awesome/4.7.0/css/font-awesome.min.css"/>
    <link rel="stylesheet" href="https://fonts.googleapis.com/css?family=Open+Sans"/>
    <link rel="stylesheet" href="https://cdn.jsdelivr.net/npm/bulma@0.9.4/css/bulma.min.css"/>
    <link rel="stylesheet" type="text/css" href="/style/style.css"/>
    <link rel="stylesheet" href="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/11.7.0/styles/base16/atelier-heath-light.min.css"/>
    <script src="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/11.7.0/highlight.min.js"></script>
    <script src="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/11.7.0/languages/fsharp.min.js"></script>
    <script>
      hljs.highlightAll();
    </script>
  </head>
  <body>
    <nav class="navbar">
      <div class="container">
        <span class="navbar-burger burger" data-target="navbarMenu">
          <span></span>
          <span></span>
          <span></span>
        </span>
        <div id="navbarMenu" class="navbar-menu">
          <a class="navbar-item" href="/">
            Home
          </a>
          <a class="navbar-item" href="/about.html">
            About
          </a>
        </div>
      </div>
    </nav>
    <section class="hero is-primary is-medium is-bold">
      <div class="hero-body">
        <div class="container has-text-centered">
          <h1 class="title">
            
          </h1>
        </div>
      </div>
    </section>
    <div class="container">
      <section class="articles">
        <div class="column is-8 is-offset-2">
          <article class="box article">
            <div class="block has-text-centered">
              <p class="title article-title">
                <a href="/posts/02-generating-chord-progressions-in-fsharp.html">
                  Generating chord progressions in F#
                </a>
              </p>
              <p class="subtitle is-8 article-subtitle">
                <i>
                  This post is part of the <a href="https://sergeytihon.com/2022/10/28/f-advent-calendar-in-english-2022/">F# Advent Calendar 2022</a>
                </i>
              </p>
              <p class="subtitle is-6 article-subtitle">
                <a href="/about.html">
                  @emneb
                </a>
                on 2022-12-10
              </p>
              <span class="tag is-primary">
                f#
              </span>
              <span class="tag is-primary">
                 .net
              </span>
              <span class="tag is-primary">
                 music theory
              </span>
            </div>
            <div class="content article-body">
              <p>Recently, I discovered a very nice blog post by Leo Cavalcante about trying to capture some musical concepts in F#: <a href="https://medium.com/@leocavalcante/music-with-f-the-language-and-the-note-403bd1fcfc31">Music with F#: The Language and the Note</a>. This prompted me to experiment a bit on my own. What follows is a slightly chaotic record of me trying to capture some basic concepts of music theory in the F# type system. The ultimate goal of this exercise was to devise an easy way to generate chord progressions for any key and mode/scale and export them into <a href="https://sonic-pi.net/">Sonic Pi</a>.</p>
<!--more-->
<h3>A Short Disclaimer</h3>
<p>The first thing to note is the definition of &quot;Music Theory&quot; that I'm applying here. For many Westerners, the &quot;rules of music&quot; that we learned or heard about are treated as <em>The</em> Music Theory. As many may suspect though, there are no iron-clad rules to music. Globally, you can see countless examples of music that does not adhere to this golden standard of &quot;how music works&quot;. Adam Neely made an excellent video about this: <a href="https://www.youtube.com/watch?v=Kr3quGh7pJA">Music Theory and White Supremacy</a> and suggests a more fitting name might be: &quot;The harmonic style of 18th-century European musicians.&quot; All this to say that, while I will be using some terminology that comes from that style, it is mainly because that's the one that I (very roughly) understand, not because it is <em>the way</em> of approaching the topic.</p>
<hr />
<h2>Notes, Chords, Scales, Progressions, oh my!</h2>
<p>Some definitions first. I'm by no means a music theorist, but for this post let's agree to the following definitions:</p>
<ul>
<li>The sound spectrum is continuous, but we subdivide it into discrete steps: notes.</li>
<li>Pitch is essentially a frequency of a sound. For example, it is agreed that the &quot;middle A&quot; pitch is 440 Hz.</li>
<li>Sound is governed by the <a href="https://en.wikipedia.org/wiki/Harmonic_series_(music)">harmonic series</a>. In the Western (and some other) traditions we give a sound the same name (note) every time the frequency doubles. This is called an <strong>octave</strong>.</li>
<li>For convenience, we decided to divide an octave into 12 equal parts, thus giving us 12 notes in an octave. The distance between these notes is a <strong>semitone</strong>.</li>
</ul>
<blockquote>
<p>Sidenote: This is actually a quite recent invention and is called <a href="https://en.wikipedia.org/wiki/12_equal_temperament">12-tone equal temperament or 12TET</a>.</p>
</blockquote>
<ul>
<li>If a set of notes is played simultaneously we call it a <strong>chord</strong></li>
<li>A scale is essentially just a set of notes (traditionally ascending in pitch).</li>
<li>A <strong>key</strong> tone is the &quot;main&quot; tone of a scale. The center to which everything seems to gravitate towards.</li>
</ul>
<p>Finally, knowing all that, what is a chord progression? It's as simple as it sounds: a sequence of chords, that is notes played together. In and of itself there is not much interesting about chord progressions. However, with time we noticed that some chords sound particularly &quot;good&quot; or &quot;right&quot; after some other chords. This is apparent in a lot of western classical, pop, or jazz music. A &quot;one-four-five&quot; is one such common progression, and a &quot;two-five&quot; is another. When practicing an instrument or as a starting point for some more creative endeavor like composing it might be nice to be able to generate any progression in any key and tell some program to play it, so this is what we'll try to do.</p>
<h2>Laying out the basics</h2>
<p>Let's start by defining our notes, there should be 12 of them.</p>
<pre><code class="language-fsharp">type Note =
    | C
    | CsDb
    | D
    | DsEb
    | E
    | F
    | FsGb
    | G
    | GsAb
    | A
    | AsBb
    | B
</code></pre>
<p>As you can see some of the notes are named more complexly than others. The base set of note names is a sequence of <code>C D E F G A B</code> (think white keys of the piano). The notes in-between (the black keys) are denoted by either decreasing the pitch of one note i.e. flattening it e.g. E -&gt; Eb or increasing i.e. sharpening it e.g. F -&gt; F#. Eb and F# are <em>enharmonically equivalent</em> which means they produce the same sound.</p>
<blockquote>
<p>Note: It wasn't always like this, the equivalence holds only in 12TET and historically they were different pitches (as dictated by the harmonic series), but we are now here and are stuck with this notation.</p>
</blockquote>
<p>These notes form a so-called chromatic scale -&gt; a set of all notes in an octave. We can represent it as a list.</p>
<pre><code class="language-fsharp">let chromaticBase = [ C; CsDb; D; DsEb; E; F; FsGb; G; GsAb; A; AsBb; B ]
</code></pre>
<p>As mentioned before, the note names repeat when they double the pitch i.e. are an octave apart. We can represent it as an infinite series.</p>
<pre><code class="language-fsharp">let tones =
    Seq.initInfinite (fun i -&gt; chromaticBase[i % chromaticBase.Length])
</code></pre>
<h2>Intervals</h2>
<p>To represent chords we can choose one of 2 approaches:</p>
<ul>
<li>as collections of notes</li>
<li>as sort of &quot;recipes&quot; encoding how to construct the chord, knowing a starting point.</li>
</ul>
<p>Representing a chord as a collection of notes is straightforward. However, complications start once you want to operate further on the chords e.g. add extensions, transpose to another key, make a minor into a major, etc. Thus, it is better, in the long run, to represent chords as relations between chord elements. Concretely, let's represent a chord as a set of distances from its root i.e. its &quot;main note&quot;.</p>
<p>These distances are called <strong>intervals</strong> and were also given names. They are usually measured in semitones.</p>
<blockquote>
<p>Note: some intervals function under multiple names. The name is dependent on the way an interval was created i.e. was it naturally occurring in a scale or was some other interval increased or decreased. That's why a Minor Third can sometimes be called an Augmented Second. I've included them as well as this terminology is actively used by musicians.</p>
</blockquote>
<pre><code class="language-fsharp">type Interval =
    | PerfectUnison
    | MinorSecond
    | MajorSecond
    | MinorThird | AugmentedSecond      // Enharmonically equivalent in 12TET
    | MajorThird
    | PerfectFourth
    | DiminishedFifth | AugmentedFourth // Enharmonically equivalent in 12TET
    | PerfectFifth
    | MinorSixth | AugmentedFifth       // Enharmonically equivalent in 12TET
    | MajorSixth | DiminishedSeventh    // Enharmonically equivalent in 12TET
    | MinorSeventh
    | MajorSeventh
    | Octave
    | OctaveUp of Interval
</code></pre>
<p>Since we don't want to list all the intervals that exceed an octave, we perform a so-called &quot;octave reduction&quot; by introducing a recursive case of <code>OctaveUp</code> which means &quot;the same interval but plus an octave&quot;.</p>
<p>Let's add a companion module with mappings between semitone values and interval names.</p>
<pre><code class="language-fsharp">module Interval =
    let rec inSemitones interval =
        match interval with
        | PerfectUnison -&gt; 0
        | MinorSecond -&gt; 1
        | MajorSecond -&gt; 2
        | AugmentedSecond
        | MinorThird -&gt; 3
        | MajorThird -&gt; 4
        | PerfectFourth -&gt; 5
        | AugmentedFourth
        | DiminishedFifth -&gt; 6
        | PerfectFifth -&gt; 7
        | AugmentedFifth
        | MinorSixth -&gt; 8
        | MajorSixth
        | DiminishedSeventh -&gt; 9
        | MinorSeventh -&gt; 10
        | MajorSeventh -&gt; 11
        | Octave -&gt; 12
        | OctaveUp interval -&gt; 12 + inSemitones interval

    let rec fromSemitones =
        function
        | 0 -&gt; PerfectUnison
        | 1 -&gt; MinorSecond
        | 2 -&gt; MajorSecond
        | 3 -&gt; MinorThird
        | 4 -&gt; MajorThird
        | 5 -&gt; PerfectFourth
        | 6 -&gt; DiminishedFifth
        | 7 -&gt; PerfectFifth
        | 8 -&gt; MinorSixth
        | 9 -&gt; MajorSixth
        | 10 -&gt; MinorSeventh
        | 11 -&gt; MajorSeventh
        | 12 -&gt; Octave
        | x when x &gt; 12 -&gt; fromSemitones (x - 12)
        | _ -&gt; failwith &quot;Can't map this&quot;
</code></pre>
<h2>Chords</h2>
<p>Having defined the intervals we can proceed to the definition of a chord. Since we've decided to define a chord as a combination of a <strong>root note</strong> and a list of intervals relative to this root, our chord could be represented as follows:</p>
<pre><code class="language-fsharp">type Chord = { Root: Note; IntervalsFromRoot: Interval list }
</code></pre>
<p>Now we can introduce some functions that construct commonly used chords. Let's start with triads i.e. chords composed of three notes. For example, a <strong>minor</strong> triad would be a Root + Minor Third + Perfect Fifth.</p>
<pre><code class="language-fsharp">module Triads =
    let major root = { Root = root; IntervalsFromRoot = [MajorThird; PerfectFifth] }
    let minor root = { Root = root; IntervalsFromRoot = [MinorThird; PerfectFifth] }
    let diminished root = { Root = root; IntervalsFromRoot = [MinorThird; DiminishedFifth] }
    let augmented root = { Root = root; IntervalsFromRoot = [MajorThird; AugmentedFifth] }
</code></pre>
<p>By having this defined we can for example construct an F#m chord like so:</p>
<pre><code class="language-fsharp">let ``F#m`` = Triads.minor FsGb
</code></pre>
<p>Having this base we can define a set of functions that modify an existing chord by adding a specific note to it.</p>
<pre><code class="language-fsharp">module Modifications =
    let with6b chord = { chord with IntervalsFromRoot = MinorSixth :: chord.IntervalsFromRoot }
    let with6 chord = { chord with IntervalsFromRoot = MajorSixth :: chord.IntervalsFromRoot }
    let withMinor7 chord = { chord with IntervalsFromRoot = MinorSeventh :: chord.IntervalsFromRoot }
    let withMajor7 chord = { chord with IntervalsFromRoot = MajorSeventh :: chord.IntervalsFromRoot }
    let withDiminished7 chord = { chord with IntervalsFromRoot = DiminishedSeventh :: chord.IntervalsFromRoot }
    let with9b chord = { chord with IntervalsFromRoot = OctaveUp(MinorSecond) :: chord.IntervalsFromRoot }
    let with9 chord = { chord with IntervalsFromRoot = OctaveUp(MajorSecond) :: chord.IntervalsFromRoot }
    let with9s chord = { chord with IntervalsFromRoot = OctaveUp(AugmentedSecond) :: chord.IntervalsFromRoot }
    let with11 chord = { chord with IntervalsFromRoot = OctaveUp(PerfectFourth) :: chord.IntervalsFromRoot }
    let with13b chord = { chord with IntervalsFromRoot = OctaveUp(MinorSixth) :: chord.IntervalsFromRoot }
    let with13 chord = { chord with IntervalsFromRoot = OctaveUp(MajorSixth) :: chord.IntervalsFromRoot }
</code></pre>
<p>This, in turn, allows us to finally construct tetrachords, that is chords composed of four notes. Triads and tetrachords are the most common types of chords in popular music like pop and jazz, so our progressions will use those.</p>
<pre><code class="language-fsharp">module TetraChords =
    open Modifications
    let seventh =  Triads.major &gt;&gt; withMinor7
    let minorSeventh = Triads.minor &gt;&gt; withMinor7
    let majorSeventh = Triads.major &gt;&gt; withMajor7
    let halfDiminished = Triads.diminished &gt;&gt; withMinor7
    let diminished = Triads.diminished &gt;&gt; withDiminished7
    let minorMajorSeventh = Triads.minor &gt;&gt; withMajor7
</code></pre>
<p>Here's how you would construct an F#maj7 chord:</p>
<pre><code class="language-fsharp">let ``F#maj7`` = TetraChords.majorSeventh FsGb
</code></pre>
<h2>Scales and modes</h2>
<p>Now that we have our chords we can generate progressions. While any sequence of chords is technically a chord progression, we're more interested in progressions that somehow relate to a scale and revolve around its key. A basic structure of a scale can be represented as a sequence of distances between its consecutive steps. For example, a chromatic scale that includes all notes within an octave could be represented as <code>[1;1;1;1;1;1;1;1;1;1;1;1]</code>. Let's define some common scales like that.</p>
<pre><code class="language-fsharp">type Scale = int list
module Scales =
    let major = [2; 2; 1; 2; 2; 2; 1]
    let harmonicMinor = [2; 1; 2; 2; 1; 3; 1]
    let melodicMinor = [2; 1; 2; 2; 2; 2; 1]
</code></pre>
<p>For completeness' sake, we can note that the minor scale is like the major scale but instead of starting on the first step you start on the fifth. This is what we refer to as modes, basically rotations of some base scale.</p>
<pre><code class="language-fsharp">    let private rotate steps list =
        List.splitAt steps list |&gt; fun (x, y) -&gt; List.append y x

    module Modes =
        let ionian = major
        let dorian = rotate 1 major
        let phrygian = rotate 2 major
        let lydian = rotate 3 major
        let mixolydian = rotate 4 major
        let aeolian = rotate 5 major
        let locrian = rotate 6 major

    let minor = Modes.aeolian

    let getNotes key (scale: Scale) =
        let indexOfRoot = Seq.findIndex (fun note -&gt; note = key) tones
        let _, notes =
            List.fold
                (fun (interval, notes) value -&gt;
                    interval+value, Seq.item (indexOfRoot + interval + value) tones :: notes)
                (0,[key])
                scale
        List.rev (List.tail notes)
</code></pre>
<p>Now we can see which notes form the F# minor scale.</p>
<pre><code class="language-fsharp">let ``F# minor`` = Scales.getNotes FsGb Scales.minor
// prints: [FsGb; GsAb; A; B; CsDb; D; E]
</code></pre>
<h2>Harmony and progressions</h2>
<p>In western tradition scales usually have 7 steps. They are numbered using Roman Numerals and were given names that describe their role in the scale. This applies also to chords built on top of those scale steps.</p>
<pre><code class="language-fsharp">type HarmonicFunction =
    | I    // Tonic
    | II   // Super-Tonic
    | III  // Mediant
    | IV   // Sub-Dominant
    | V    // Dominant
    | VI   // Sub-Mediant
    | VII  // Leading tone
</code></pre>
<p>An example progression that we are interested in would be a <code>I-IV-V-I</code> i.e. tetrachords built on top of the first, fourth, fifth, and first (again) steps of a scale. To build a chord on a scale we need to only use the notes that are in the scale. This means that if we are building a triad and a third from its root happens to be a Minor Third, then our chord will be minor or diminished (depending on what kind of Fifth we will get from our scale).</p>
<p>First, we need a function that builds a tetrachord from a given step of a given scale.</p>
<pre><code class="language-fsharp">module Harmony =
    // map a harmonic function to an index
    let mapToScaleIndex =
        function
        | I -&gt; 0
        | II -&gt; 1
        | III -&gt; 2
        | IV -&gt; 3
        | V -&gt; 4
        | VI -&gt; 5
        | VII -&gt; 6

    // this converts a scale from a representation of intervals between steps to intervals to root (key).
    let private getNotesWithIntervals key (scale: Scale) =
        let indexOfRoot = Seq.findIndex (fun note -&gt; note = key) tones
        let _, notes =
            List.fold
                (fun (interval, notes) value -&gt;
                    interval+value, (Seq.item (indexOfRoot + interval + value) tones, value) :: notes)
                (0,[key, scale[indexOfRoot]])
                scale
        List.rev (List.tail notes)

    // helper function to turn a scale into an infinite sequence
    let private infinite (scale: _ list) =
        Seq.initInfinite (fun i -&gt; scale[i % scale.Length])

    //
    let buildTetrachord key scale harmonicFunction =
        let scaleNotes = getNotesWithIntervals key scale |&gt; infinite
        // find the first occurence of the key note
        let indexOfKey = Seq.findIndex (fun (note, _) -&gt; note = key) scaleNotes
        // find the index of the chord root and determine the root note
        let indexOfChordRoot = indexOfKey + (mapToScaleIndex harmonicFunction)
        let rootNote, _ = Seq.item indexOfChordRoot scaleNotes

        // recursively find the first 4 stacked thirds that form a tetrachord
        // the function also accumulates semitone distances to the root to determine the right interval
        let rec getChordNotes start index totalInterval notes =
            if List.length notes &gt;= 4 then
                notes
            else
                let note, interval = Seq.item (start + index) scaleNotes
                let semitonesFromRoot = totalInterval + interval
                if index % 2 = 0 then
                    getChordNotes start (index + 1) semitonesFromRoot ((note, semitonesFromRoot) :: notes)
                else
                    getChordNotes start (index + 1) semitonesFromRoot notes
        let chordNotes = getChordNotes indexOfChordRoot 1 0 [ rootNote, 0 ] |&gt; List.rev

        // convert to `Chord`
        match chordNotes with
        | (root, _) :: rest -&gt;
            { Root = root
              IntervalsFromRoot =
                [ for (_ ,interval) in rest do
                    Interval.fromSemitones interval ]}
        | _ -&gt; failwith &quot;&quot;
</code></pre>
<p>Having that, building a progression is only a matter of mapping:</p>
<pre><code class="language-fsharp">    let buildTetrachordProgression key scale progression =
        progression
        |&gt; List.map (buildTetrachord key scale)
</code></pre>
<p>Finally, we can build our <code>I-IV-V-I</code> in F# major:</p>
<pre><code class="language-fsharp">let ``I-IV-V-I`` = Harmony.buildTetrachordProgression FsGb Scales.major [I; IV; V; I]
(*
    Results in
    val ``I-IV-V-I`` : Chord list =
        [{ Root = FsGb
            IntervalsFromRoot = [MajorThird; PerfectFifth; MajorSeventh] };
        { Root = B
            IntervalsFromRoot = [MajorThird; PerfectFifth; MajorSeventh] };
        { Root = CsDb
            IntervalsFromRoot = [MajorThird; PerfectFifth; MinorSeventh] };
        { Root = FsGb
            IntervalsFromRoot = [MajorThird; PerfectFifth; MajorSeventh] }]
*)
</code></pre>
<p>This has created a progression of F#maj7-&gt;Bmaj7-&gt;C#7-&gt;F#maj7. If we want to create an analogous progression but in minor, it's as easy as changing the scale and we get a progression of F#m7-&gt;Bm7-&gt;C#m7-&gt;F#m7.</p>
<pre><code class="language-fsharp">let ``I-IV-V-I`` =
    Harmony.buildTetrachordProgression FsGb Scales.minor [I; IV; V; I]
(*
    Results in
    val ``I-IV-V-I`` : Chord list =
        [{ Root = FsGb
            IntervalsFromRoot = [MinorThird; PerfectFifth; MinorSeventh] };
        { Root = B
            IntervalsFromRoot = [MinorThird; PerfectFifth; MinorSeventh] };
        { Root = CsDb
            IntervalsFromRoot = [MinorThird; PerfectFifth; MinorSeventh] };
        { Root = FsGb
            IntervalsFromRoot = [MinorThird; PerfectFifth; MinorSeventh] }]
*)
</code></pre>
<hr />
<h2>Bonus: let's listen to it!</h2>
<p>Being able to generate these progressions is a nice exercise, but unless we can listen to them somehow it is still of limited value. Here's where <a href="https://sonic-pi.net/">Sonic Pi</a> comes in. It is a music creation tool that is code-based, which makes it a natural fit for our use case here.</p>
<p>In a basic scenario to play a note in Sonic Pi one has to write a <code>play</code> command, followed by the desired note. Additionally, we should attach a number denoting which octave we want the note to be played in. For example, the command to play the &quot;middle A&quot; would be <code>play :A4</code>. The <code>play</code> command can also take a collection of notes to play (<code>play [:Fs4,:As4,:Cs5,:F5]</code>), which is exactly what we need to play some chords! Let's add some functions to help us print a series of commands for Sonic PI based on our chord progression:</p>
<pre><code class="language-fsharp">module SonicPi =
    let baseOctave = 4
    let toSonicPiNote (note, octaveDiff) =
        match note with
        | C -&gt; &quot;C&quot;
        | CsDb -&gt; &quot;Cs&quot;
        | D -&gt; &quot;D&quot;
        | DsEb -&gt; &quot;Ds&quot;
        | E -&gt; &quot;E&quot;
        | F -&gt; &quot;F&quot;
        | FsGb -&gt; &quot;Fs&quot;
        | G -&gt; &quot;G&quot;
        | GsAb -&gt; &quot;Gs&quot;
        | A -&gt; &quot;A&quot;
        | AsBb -&gt; &quot;As&quot;
        | B -&gt; &quot;B&quot;
        |&gt; fun n -&gt; sprintf &quot;:%s%i&quot; n (baseOctave + octaveDiff)
    let printChord chord  =
        Chord.getNotes chord
        |&gt; List.map toSonicPiNote
        |&gt; fun notes -&gt; $&quot;play [{String.Join(',', notes)}]&quot;

    let printChordProgression stepDuration chords =
        chords
        |&gt; List.map printChord
        |&gt; List.map (fun chord -&gt;
            $&quot;{chord}, release: {stepDuration}{Environment.NewLine}sleep {stepDuration}&quot;)
        |&gt; fun chords -&gt; String.Join(Environment.NewLine, chords)
</code></pre>
<p>Let's convert our chord progression to the Sonic Pi commands and paste it to a <code>live_loop</code> in Sonic Pi.</p>
<pre><code class="language-fsharp">``I-IV-V-I``
|&gt; SonicPi.printChordProgression 2
|&gt; printfn &quot;%s&quot;
(*
    play [:Fs4,:As4,:Cs5,:F5], release: 2
    sleep 2
    play [:B4,:Ds5,:Fs5,:As5], release: 2
    sleep 2
    play [:Cs4,:F4,:Gs4,:B4], release: 2
    sleep 2
    play [:Fs4,:As4,:Cs5,:F5], release: 2
    sleep 2
*)
</code></pre>
<p><img src="/images/sonicpi.png" alt="I-IV-V-I chord progression pasted into a Sonic Pi loop" /></p>
<p>And here is how it sounds:</p>
<audio controls>
  <source src="/assets/recording.ogg" type="audio/ogg">
Your browser does not support the audio element.
</audio>
<hr />
<h2>Summary</h2>
<p>I hope you enjoyed this journey through music theory and F#. The code I wrote here is far from elegant/optimal but I found trying to fit all those concepts into code very enjoyable. I would still like to continue adding new features to make this more flexible and powerful. From the top of my head this would be things like:</p>
<ul>
<li>Requesting a specific type of chord for a harmonic function: for example specifying V to be the dominant seventh chord regardless of what the scale would suggest (specifying <code>V7</code> instead of <code>V</code>).</li>
<li>Chord substitutions: e.g. replace a chord in a progression with a 2-5 that resolves to that chord (jazz reharmonization)</li>
<li>Reverse chord identification: a set of Active Patterns to identify whether a chord is Minor, Major, etc.</li>
<li>Drop the bass note by an octave or two.</li>
<li>Support chord inversions.</li>
<li>and more!</li>
</ul>
<p>You can find all the code from here in this <a href="https://gist.github.com/mnebes/8cfe529467cfd62ed500bb2ac8b57255">gist</a>. Feel free to play with it!</p>
<hr />

            </div>
            <div class="block"></div>
          </article>
        </div>
      </section>
    </div>
    <script src="/js/nav.js"></script>
  </body>
</html>